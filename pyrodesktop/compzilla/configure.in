
AC_INIT(compzilla, 0.2, pyrodesktop@googlegroups.com)

AC_CONFIG_SRCDIR(compzilla/chrome.manifest)
AC_CONFIG_HEADERS(config.h)

AM_INIT_AUTOMAKE

AM_MAINTAINER_MODE
AM_DISABLE_STATIC
AM_PROG_LIBTOOL
AC_PROG_CC
AC_PROG_INSTALL


PKG_CHECK_MODULES(GDK, gdk-2.0 >= 2.8.0)

PKG_CHECK_MODULES(NSPR, firefox-nspr >= 1.5.0)
AC_SUBST(NSPR_LIBS)
AC_SUBST(NSPR_CFLAGS)

FIREFOX_XPCOM="firefox-xpcom >= 1.5.0"

GECKO_IDLDIR=`$PKG_CONFIG --variable=idldir "$FIREFOX_XPCOM"`
GECKO_LIBDIR=`$PKG_CONFIG --variable=libdir "$FIREFOX_XPCOM"`
GECKO_INCLUDEDIR=`$PKG_CONFIG --variable=includedir "$FIREFOX_XPCOM"`
GECKO_EXEC_PREFIX=`$PKG_CONFIG --variable=exec_prefix "$FIREFOX_XPCOM"`

## Installed firefox has xpidl in lib/firefox directory
XPIDL="$GECKO_LIBDIR"/xpidl
if ! test -f "$XPIDL"; then
   ## Built firefox has xpidl in bin directory
   XPIDL="$GECKO_EXEC_PREFIX"/xpidl
   if ! test -f "$XPIDL"; then
      AC_MSG_ERROR([Cannot find xpidl executable])
   fi
fi

## Installed firefox has firefox in bin directory
FIREFOX="$GECKO_EXEC_PREFIX"/bin/firefox
if ! test -f "$FIREFOX"; then
   ## Built firefox has firefox in exec_prefix
   FIREFOX="$GECKO_EXEC_PREFIX"/firefox
   if ! test -f "$FIREFOX"; then
      AC_MSG_ERROR([Cannot find firefox executable])
   fi
fi

AC_SUBST(GECKO_IDLDIR)
AC_SUBST(GECKO_LIBDIR)
AC_SUBST(GECKO_INCLUDEDIR)
AC_SUBST(GECKO_EXEC_PREFIX)
AC_SUBST(XPIDL)
AC_SUBST(FIREFOX)


AC_CACHE_CHECK(if Gecko was compiled to use Thebes, ac_cv_gecko_thebes, [
	CPPFLAGS_save="$CPPFLAGS"
	CPPFLAGS="$CPPFLAGS -I${GECKO_INCLUDEDIR}"
	AC_EGREP_CPP(yes, [
		#include "mozilla-config.h"
		#ifdef MOZ_CAIRO_GFX
		yes
		#endif
	], ac_cv_gecko_thebes=yes, ac_cv_gecko_thebes=no)
	CPPFLAGS="$CPPFLAGS_save"
])

AM_CONDITIONAL(GECKO_USE_THEBES, test "x$ac_cv_gecko_thebes" = "xyes")


##
## These are used in generated install.rdf
##

if test "x$ac_cv_gecko_thebes" = "xyes"; then
   FIREFOX_MINVERSION="3.0a8pre"
   FIREFOX_MAXVERSION="3.0a8"
   FIREFOX_SHORTNAME="minefield"
else
   FIREFOX_MINVERSION="2.0"
   FIREFOX_MAXVERSION="2.0.0.*"
   FIREFOX_SHORTNAME="ff2"
fi
FIREFOX_TARGETPLATFORM="Linux_x86-gcc3" # FIXME!

AC_SUBST(FIREFOX_MINVERSION)
AC_SUBST(FIREFOX_MAXVERSION)
AC_SUBST(FIREFOX_SHORTNAME)
AC_SUBST(FIREFOX_TARGETPLATFORM)


##
## Checks for needed Xextensions
##
PKG_CHECK_MODULES(XEXTENSIONS, xcomposite xdamage)


AC_OUTPUT([
Makefile
compzilla/Makefile
compzilla/compzilla
compzilla/install.rdf
])


dnl ==========================================================================
echo "

        Compzilla $VERSION
	==================

        prefix:                   ${prefix}
        compiler:                 ${CC}
	xpidl:			  ${XPIDL}
        firefox:                  ${FIREFOX}
	Gecko includes:		  ${GECKO_INCLUDEDIR}
	Gecko idl files:	  ${GECKO_IDLDIR}
	Gecko using thebes:       ${ac_cv_gecko_thebes}
        XEXTENSIONS_CFLAGS:       ${XEXTENSIONS_CFLAGS}
        XEXTENSIONS_LIBS:         ${XEXTENSIONS_LIBS}
        GDK_CFLAGS:               ${GDK_CFLAGS}

        Now type 'make' to build $PACKAGE
"
